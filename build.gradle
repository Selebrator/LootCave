apply plugin: 'java'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        name = 'JOpt-repo'
        url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    }
}

dependencies {
    compileOnly 'org.bukkit:bukkit:1.13.2-R0.1-SNAPSHOT'
    compile 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'
}

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

jar {
    ext.plugin_version = config.plugin_version
    if(plugin_version.endsWith('-dev')) {
        plugin_version = plugin_version + "." + new Date().format('yyyyMMdd.HHmmss')
    }
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.compileOnly).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

group = config.plugin_group + config.plugin_id
version = project.jar.plugin_version

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "plugin_version", project.jar.plugin_version

    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'

        //replace placeholders
        expand(
                'plugin_name': project.config.plugin_name,
                'plugin_version': project.jar.plugin_version,
                'author': project.config.author
        )
    }

    //copy everything else
    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}
